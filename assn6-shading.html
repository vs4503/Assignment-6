<html>
<head>
    <title>CSCI-510: Assn 6 - Materials and Shading</title>
    
    <!-- vertex Shader -->
    <script id="phong-per-vertex-V"  type="x-shader/x-vertex">
    #version 300 es
    precision mediump float;
    // Vertex shader for phong illumination model
    // Per vertex shading

    // Vertex Attributes
    in vec3 aVertexPosition; // in model coords
    in vec3 aNormal;    // in model coords

    // outputs
    out vec3 theColor;
      
    // Transforms
    uniform mat4 modelT;
    uniform mat4 viewT;
    uniform mat4 projT;

    // Light parameters
    uniform vec3 ambientLight;
    uniform vec3 lightPosition;   // in world coords
    uniform vec3 lightColor;

    // object color parameters
    uniform vec3 baseColor;
    uniform vec3 specHighlightColor;

    // Phong parameters
    uniform float ka;
    uniform float kd;
    uniform float ks;
    uniform float ke;
      
    void main()
    {
        // All calculations will be done in camera space
        mat4 modelView = viewT * modelT;
        mat4 normalmatrix = transpose(inverse (modelView));
        
        vec3 vcam = (modelView * vec4(aVertexPosition, 1.0)).xyz;
        vec3 lcam = (viewT * vec4(lightPosition, 1.0)).xyz;
        vec3 ncam = (normalmatrix * vec4(aNormal, 1.0)).xyz;
        ncam = faceforward (ncam, vcam, ncam);
        
        
        vec3 N = normalize (ncam);
        vec3 L = normalize (lcam - vcam);
        vec3 R = normalize (reflect (-L, N));
        vec3 V = -normalize (vcam);
        
        // individual components
        vec3 ambient = ka * ambientLight * baseColor;
        vec3 diffuse = kd * lightColor * baseColor * max (dot(L, N), 0.0);
        vec3 spec = ks * specHighlightColor * lightColor * pow (max(dot(R, V), 0.0), ke);
        
        // final color
        theColor = ambient + diffuse + spec;
    
        
        // transform vertex to clip space
        gl_Position = projT * viewT * modelT * vec4 (aVertexPosition, 1.0);
    }

    </script>

    <script id="phong-per-fragment-V"  type="x-shader/x-vertex">
    #version 300 es
    precision mediump float;
    // Vertex shader for phong illumination model
    // Per vertex shading

    // Vertex Attributes
    in vec3 aVertexPosition; // in model coords
    in vec3 aNormal;    // in model coords

    // outputs
    out vec3 N;
    out vec3 L;
    out vec3 V;
      
    // Transforms
    uniform mat4 modelT;
    uniform mat4 viewT;
    uniform mat4 projT;

    // Light parameters
    uniform vec3 ambientLight;
    uniform vec3 lightPosition;   // in world coords
    uniform vec3 lightColor;

    // object color parameters
    uniform vec3 baseColor;
    uniform vec3 specHighlightColor;

    // Phong parameters
    uniform float ka;
    uniform float kd;
    uniform float ks;
    uniform float ke;
      
    void main()
    {
        // All calculations will be done in camera space
        mat4 modelView = viewT * modelT;
        mat4 normalmatrix = transpose(inverse (modelView));
        
        vec3 vcam = (modelView * vec4(aVertexPosition, 1.0)).xyz;
        vec3 lcam = (viewT * vec4(lightPosition, 1.0)).xyz;
        vec3 ncam = (normalmatrix * vec4(aNormal, 1.0)).xyz;
        ncam = faceforward (ncam, vcam, ncam);
        
        // vectors to pass on to Fragment Shader
        N = normalize (ncam);
        L = normalize (lcam - vcam);
        V = -normalize (vcam);
    
        // transform vertex to clip space
        gl_Position = projT * viewT * modelT * vec4 (aVertexPosition, 1.0);
    }

    </script>

     <!-- fragment Shader -->
     <script id="phong-per-fragment-F" type="x-shader/x-fragment">
         #version 300 es

         // Fragment shader for phong illumination model
         // Per vertex shading


         precision mediump float;
             
         // calculated by vertex shader and passsed to fragment
         in vec3 N;
         in vec3 L;
         in vec3 V;
         
         // Light parameters
         uniform vec3 ambientLight;
         uniform vec3 lightColor;

         // object color parameters
         uniform vec3 baseColor;
         uniform vec3 specHighlightColor;

         // Phong parameters
         uniform float ka;
         uniform float kd;
         uniform float ks;
         uniform float ke;

         // Color that is the result of this shader
         out vec4 fragColor;

         void main(void) {
             // individual components
             vec3 R = normalize (reflect (-L, N));
             
             vec3 ambient = ka * ambientLight * baseColor;
             vec3 diffuse = kd * lightColor * baseColor * max (dot(L, N), 0.0);
             vec3 spec = ks * specHighlightColor * lightColor * pow (max(dot(R, V), 0.0), ke);
             
             // final color
             fragColor = vec4 (ambient + diffuse + spec, 1.0);
         }

    </script>
     
     <script id="phong-per-vertex-F" type="x-shader/x-fragment">
          #version 300 es

          // Fragment shader for phong illumination model
          // Per fragment shading


          precision mediump float;
              
          in vec3 theColor;

          // Color that is the result of this shader
          out vec4 fragColor;

          void main(void) {
              fragColor = vec4 (theColor.xyz, 1.0);
              //fragColor = vec4 (1.0, 0.0, 0.0, 1.0);
          }

     </script>
     
     <!-- Matrix library and code for standard transformations -->
     <script type="text/javascript" src="gl-matrix-min.js"></script>
     
     <!-- include the shape creation functions -->
    <script type="text/javascript" src="./cgIShape.js"></script>
    
    <!-- definition of standard shapes -->
    <script type="text/javascript" src="./myShapes-min.js"></script>
    
    <!-- include the main tesselation functions -->
    <script type="text/javascript" src="./shadeMain.js"></script>
    
    <!-- keyboard functions -->
    <script type="text/javascript">
    
    function gotKey (event) {
        
        var key = event.key;
        
        //  incremental rotation
        if (key == 'v') {
            nowShowing = 'Vertex';
            draw();
        }
        
        if (key == 'f') {
            nowShowing = 'Fragment';
            draw();
        }
        
    }
    
    </script>

  <script type="text/javascript">
    // Call init once the webpage has loaded
    window.onload = init;
  </script>
</head>

<body>
  <h1>CSCI-510: Assn 6 - Materials and Shading</h1>
  <table>
      <tr>
          <td><canvas id="webgl-canvas" width="500" height="500">
            Your browser does not support the HTML5 canvas element.
          </canvas></td>
          
          <td>
              <h3>Controls</h3>
              
              <table border="1">
              <tbody>
            
              <tr>
              <td>v</td>
              <td>Render using per-vertex shaders</td>
              </tr>
              <tr>
              <td>f</td>
              <td>Render using per-fragment shaders</td>
              </tr>
              </tbody>
              </table>
              
          </td>
      </tr>
  </table>
  
  

</body>
</html>

